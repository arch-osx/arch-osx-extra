pkgname=nasm
pkgver=2.12.02
pkgrel=1
pkgdesc="An 80x86 assembler designed for portability and modularity"
arch=('i686' 'x86_64')
url="http://www.nasm.us"
license=('BSD')
depends=('libc')
source=(http://www.nasm.us/pub/nasm/releasebuilds/${pkgver}/${pkgname}-${pkgver}.tar.xz)
sha1sums=('a0e6e655d04009178d4b7c15d935a46a691a617c')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/opt/arch
  make
  make -C doc info
}

package() {
  cd ${pkgname}-${pkgver}
  make INSTALLROOT="${pkgdir}" install install_rdf
  install -dm755 "${pkgdir}"/opt/arch/share/info
  install -m644 doc/info/* "${pkgdir}"/opt/arch/share/info/
  install -d -m755 "${pkgdir}"/opt/arch/share/licenses/nasm
  install -m644 LICENSE "${pkgdir}"/opt/arch/share/licenses/nasm/LICENSE
}
