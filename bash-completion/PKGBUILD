pkgname=bash-completion
pkgver=2.3
pkgrel=1
pkgdesc="Programmable completion for the bash shell"
arch=('any')
url="https://github.com/scop/bash-completion"
license=('GPL2')
depends=('bash')
options=('!emptydirs' '!makeflags')
source=("https://github.com/scop/bash-completion/releases/download/2.3/bash-completion-2.3.tar.xz"
        'bash-4.3.patch')
sha1sums=('6b42d6747dab48bdc983627e8ce69283ff0e6b79'
          'd2a01350990c1221bd53d55edf31210c74d02bca')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ../bash-4.3.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr/local --sysconfdir=/usr/local/etc
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

# bash-completion is sourced in /usr/local/etc/bash.bashrc so that non-bash shell don't source it
  rm "${pkgdir}/usr/local/etc/profile.d/bash_completion.sh"

# remove Slackware's makepkg completion
  rm "${pkgdir}/usr/local/share/bash-completion/completions/makepkg"

# remove completions which overlap with util-linux
  rm "${pkgdir}/usr/local/share/bash-completion/completions"/{u,}mount
}
