pkgname=jemalloc
pkgver=4.4.0
pkgrel=1
pkgdesc='General-purpose scalable concurrent malloc implementation'
arch=('x86_64')
license=('BSD')
url='http://www.canonware.com/jemalloc/'
depends=('libc')
#provides=('libjemalloc.so')
optdepends=('perl: for jeprof')
source=(https://github.com/jemalloc/jemalloc/releases/download/$pkgver/$pkgname-$pkgver.tar.bz2)
md5sums=('81b59778e19696d99e2f7922820671b0')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/opt/arch
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  find "$pkgdir" -name \*.a -type f -exec chmod 644 '{}' \;

  install -dm755 "$pkgdir/opt/arch/share/licenses/$pkgname"
  install -m644 COPYING "$pkgdir/opt/arch/share/licenses/$pkgname/COPYING"
}
