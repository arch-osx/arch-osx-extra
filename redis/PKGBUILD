pkgname=redis
pkgver=3.0.7
pkgrel=1
pkgdesc='Advanced key-value store'
arch=('x86_64')
url='http://redis.io/'
license=('BSD')
#depends=('jemalloc' 'grep' 'shadow')
depends=('jemalloc' 'grep')
backup=('usr/local/etc/redis.conf')
install=redis.install
source=(http://download.redis.io/releases/redis-$pkgver.tar.gz
        redis.conf-sane-defaults.patch
        redis-2.8.11-use-system-jemalloc.patch
        org.arch-osx.redis.plist
        )
md5sums=('84ed3f486e7a6f0ebada6917370f3532'
         'd8e38d4d8676c898bdca4bbcb3ea2c3a'
         'c0a6beb4c66d80ad39aed13494d7bfe6'
         '003b3cb719b058be518cf5141f2e2483')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i "$srcdir"/redis.conf-sane-defaults.patch
  patch -p1 -i "$srcdir"/redis-2.8.11-use-system-jemalloc.patch
}

build() {
  make -C $pkgname-$pkgver
}

# pops up lots of prompts
#check() {
#  make -C $pkgname-$pkgver test
#}

package() {
  cd $pkgname-$pkgver
  make PREFIX="$pkgdir"/usr/local install

  install -dm755 "$pkgdir"/usr/local/share/licenses/redis
  install -m644 COPYING "$pkgdir"/usr/local/share/licenses/redis/LICENSE
  install -dm755 "$pkgdir"/usr/local/etc
  install -m644 redis.conf "$pkgdir"/usr/local/etc/redis.conf

  ln -sf redis-server $pkgdir/usr/local/bin/redis-sentinel

  mkdir -p "$pkgdir"/Library/LaunchDaemons
  install -m644 "$srcdir"/org.arch-osx.redis.plist "$pkgdir"/Library/LaunchDaemons
}
